apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "io.github.andreybgm.gigreminder"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_ENDPOINT", '"https://kudago.com/public-api/v1.3/"'

        setProperty("archivesBaseName", "gigreminder-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    productFlavors {
        prod {

        }

        mock {
            applicationIdSuffix ".mock"
            versionNameSuffix "-mock"
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def supportLibraryVersion = '25.4.0'
    def constraintLayoutVersion = '1.0.2'

    def rxJavaVersion = '2.1.0'
    def rxAndroidVersion = '2.0.1'
    def rxJava2InteropVersion = '0.10.0'
    def sqlBriteVersion = '1.1.1'

    def retrofitVersion = '2.2.0'
    def okhttpVersion = '3.6.0'
    def glideVersion = '3.8.0'

    def butterKnifeVersion = '8.6.0'

    def junitVersion = '4.12'
    def assertjVersion = '2.8.0'
    def mockitoVersion = '2.8.9'
    def espressoVersion = '2.2.2'

    def leakCanaryVersion = '1.5.1'

    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:support-v13:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "com.github.akarnokd:rxjava2-interop:$rxJava2InteropVersion"
    compile "com.squareup.sqlbrite:sqlbrite:$sqlBriteVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    compile "com.github.bumptech.glide:glide:$glideVersion"

    compile "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"

    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile "org.assertj:assertj-core:$assertjVersion"

    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
}
